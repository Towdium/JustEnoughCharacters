architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${parent.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${parent.fabric_api_version}"

    common(project(path: ":${parent.mc_version}:common", configuration: "namedElements"))
    shadowCommon(project(path: ":${parent.mc_version}:common", configuration: "transformProductionFabric")) { transitive = false }

    modImplementation "curse.maven:jei-238222:4593546"
}

processResources {
    inputs.property "version", project(":").version

    filesMatching("fabric.mod.json") {
        expand "version": project(":").version
    }
}

shadowJar {
    exclude "architectury.common.json"
    exclude "mods.toml"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":${parent.mc_version}:common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

