buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'

version = "1.16.5-${verspec}.${verbuild}"
group = "me.towdium.jecharacters"
archivesBaseName = "jecharacters"

sourceCompatibility = targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven { url 'https://modmaven.dev' }
    maven { url 'https://jitpack.io' }
}

configurations {
    shade
    compile.extendsFrom shade
}

minecraft {
    mappings channel: 'snapshot', version: '20200916-1.16.2'
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            mods {
                jecharacters {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            mods {
                jecharacters {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'jecharacters', '--all', '--output', file('src/generated/resources/')
            mods {
                jecharacters {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    shade "com.github.towdium:pinin:${verpinin}"
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.39'
    implementation fg.deobf('mezz.jei:jei-1.16.5:7.8.0.1009')
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.2.0'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes([
                "Specification-Title": 'Just Enough Characters',
                "Specification-Vendor": 'Towdium',
                "Specification-Version": "${verspec}",
                "Implementation-Title": project.name,
                "Implementation-Version": "${verspec}.${verbuild}",
                "Implementation-Vendor" : 'Towdium',
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            include 'me/towdium/pinin/**'
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task generate(type:Exec) {
    commandLine 'python', 'generate.py'
}

compileJava.dependsOn generate

task replaceResources(type: Copy) {
    outputs.upToDateWhen { false }
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'
        expand 'version': version
    }
    into "$buildDir/resources/main/"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
    finalizedBy replaceResources
}
